apply plugin: 'java'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
}

jar {
    manifest {
        attributes 'Implementation-Vendor': 'Alexander Burchak',
                'Implementation-Title': 'PasswoGram Generator Implementations',
                'Implementation-Version': version,
                'Built-Date': builtDate,
                'Built-By': builtBy,
                'Built-JDK': builtJDK,
                'Built-Host': builtHost
    }
}

dependencies {
    compile project(":generator-api")

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    compile "org.springframework.boot:spring-boot-starter"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.apache.httpcomponents:httpclient:${httpClientVersion}"

    testCompile "org.testng:testng:${testngVersion}"
    testCompile ("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "junit", module: "junit"
    }
}

sourceSets {
    itest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java {
            srcDir file('src/itest/java')
        }
    }
}

configurations {
    itestCompile.extendsFrom testCompile
    itestRuntime.extendsFrom testRuntime
}

task itest(type: Test) {
    description = 'Run integration tests'
    group "Verification"
    dependsOn test

    useTestNG()
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    // jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=5005'
}

test.finalizedBy itest
